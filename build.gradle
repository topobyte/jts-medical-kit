task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = pGroup
version = pVersion

if (project.hasProperty("topobyte")) {
    apply from: 'file:///' + System.getProperty("user.home") \
        + '/.gradle/topobyte.gradle'
}

repositories {
    maven {
        url 'http://mvn.topobyte.de'
    }
    maven {
        url 'http://mvn.slimjars.com'
    }
    mavenCentral()
}

dependencies {
    compile 'de.topobyte:jsi:1.2.0'
    compile 'de.topobyte:jsi-and-jts:0.1.0'
    compile 'com.vividsolutions:jts:1.13'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

def pomInfo = new XmlSlurper().parse(new File('info.pom'));

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name pomInfo.name
                    description pomInfo.description
                    url pomInfo.url
                }
            }
        }
    }
}
